# general
create path (sling:OrderedFolder) /content

# sling-readall
create service user sling-readall with path system/sling

set principal ACL for sling-readall
    allow   jcr:read    on /
end

# sling-xss
create service user sling-xss with path system/sling

create path (sling:Folder) /apps/sling/xss

set principal ACL for sling-xss
    allow   jcr:read    on /apps/sling/xss
end

# sling-jcr-install
create service user sling-jcr-install with path system/sling

# used for config OSGi writeback
create path (sling:Folder) /apps/sling/install

set principal ACL for sling-jcr-install
    allow    rep:write    on /apps/sling/install
end

# content-package installer
create service user sling-package-install with path system/sling

set principal ACL for sling-package-install
    allow   jcr:all     on    /
    allow   jcr:namespaceManagement,jcr:nodeTypeDefinitionManagement on :repository
end
#<<< SLING-5848 - Define service user and ACLs for Scripting
create service user sling-search-path-reader with path system/sling

create path (sling:Folder) /libs
create path (sling:Folder) /apps

set principal ACL for sling-search-path-reader
    allow   jcr:read    on /libs,/apps
end
# SLING-5848 - Define service user and ACLs for Scripting >>>
#<<< SLING-9735 - Define service user and ACLs for jcr.contentloader
create service user sling-jcr-content-loader with path system/sling
set principal ACL for sling-jcr-content-loader
    allow jcr:all on /
end
# SLING-9735 - Define service user and ACLs for jcr.contentloader >>>
#<<< SLING-9809 - Define service user and ACLs for jcr.usermanager
create service user sling-jcr-usermanager with path system/sling
set principal ACL for sling-jcr-usermanager
    allow jcr:read,jcr:readAccessControl,jcr:modifyAccessControl,rep:write,rep:userManagement on /home
end
# SLING-9809 - Define service user and ACLs for jcr.usermanager >>>

# SLING-10597 - Simplify setup of resource resolution mappings
create path (sling:Folder) /etc/map
create path (sling:Folder) /etc/map/http

# eventing
create service user sling-event with path system/sling
create path (sling:Folder) /var
create path (sling:Folder) /var/eventing
set principal ACL for sling-event
    allow   jcr:read,rep:write    on /var/eventing
end

# discovery
create service user sling-discovery with path system/sling
create path (sling:Folder) /var/discovery
create path (sling:Folder) /var/discovery/oak
set principal ACL for sling-discovery
    allow   jcr:read,rep:write    on /var/discovery
end

set ACL for everyone
    deny jcr:all on :repository,/,/content
end

create path / with properties
  set sling:resourceType to sling:redirect
  set sling:target to /system/console
end

create service user sling-rocket-admin with forced path /home/users/system/sling-rocket-users
set ACL for sling-rocket-admin
    allow jcr:all on :repository,/
end
